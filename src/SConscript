#
# Copyright 2010-2014 Fabric Software Inc. All rights reserved.
#
Import('parentEnv', 'kl2edk', 'kl', 'extSuffix', 'STAGE_DIR')

extName = 'Oculus'

env = parentEnv.Clone()
env.Append(CPPPATH = [env.Dir('.').abspath])
env.Append(LIBS = ['libovr64'])
env.Append(LIBS = ['libboost_thread-vc100-mt-s-1_55'])
env.Append(CCFLAGS = ['/EHsc'])

env.Append(LIBS = [
  'ws2_32.lib',
  'opengl32.lib',
  'Dbghelp.lib',
  'winmm.lib',
  'kernel32.lib',
  'user32.lib',
  'gdi32.lib',
  'winspool.lib',
  'comdlg32.lib',
  'advapi32.lib',
  'shell32.lib',
  'ole32.lib',
  'oleaut32.lib',
  'uuid.lib',
  'odbc32.lib',
  'odbccp32.lib'
])

sources = [
  env.File('Oculus.fpm.json')
]
sources += env.Glob('*.kl')

cppFiles = [
  env.File('ORDevice_functions.cpp')
]

extensionFiles = env.Install(STAGE_DIR, [env.File(extName+'.fpm.json')] + env.Glob('*.kl'))
kl2edkResults = env.RunKL2EDK(cppFiles, sources)

extLibFileName = env.File(extName + '-' + extSuffix)
libraryFiles = Flatten([env.SharedLibrary(extLibFileName, cppFiles)])
env.Depends(libraryFiles, kl2edkResults)

extensionFiles += env.Install(STAGE_DIR, libraryFiles[0])

Return('extensionFiles')
